# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Jonathan Steel <jsteel@aur.archlinux.org>
# Contributor: Justin Davis (juster) <jrcd83@gmail.com>

pkgname=perl-package-deprecationmanager
pkgver=0.17
pkgrel=8
pkgdesc="Manage deprecation warnings for your distribution"
arch=('any')
license=('Artistic2.0')
depends=('perl-params-util' 'perl-sub-install' 'perl-sub-name' 'perl-package-stash'
         'perl-module-implementation' 'perl-namespace-autoclean')
checkdepends=('perl-test-fatal' 'perl-test-warnings')
options=('!emptydirs')
url="https://metacpan.org/release/Package-DeprecationManager"
source=(https://search.cpan.org/CPAN/authors/id/D/DR/DROLSKY/Package-DeprecationManager-$pkgver.tar.gz)
sha256sums=('1d743ada482b5c9871d894966e87d4c20edc96931bb949fb2638b000ddd6684b')

build() {
  cd "$srcdir"/Package-DeprecationManager-$pkgver
  perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd "$srcdir"/Package-DeprecationManager-$pkgver
  make test
}

package() {
  cd "$srcdir"/Package-DeprecationManager-$pkgver
  make install DESTDIR="$pkgdir"
}
