# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Maintainer: Santiago Torres-Arias <santiago[at]archlinux[dot]org>
# Contributor: Aaron McDaniel (mcd1992) <'aur' at the domain 'fgthou.se'>

pkgname=r2cutter
_gitcommit=86ee0a9caedc9d515d62738d4b33aaa9e346ac2f
pkgver=1.12.0
epoch=1
pkgrel=3
pkgdesc='Qt and C++ GUI for radare2 reverse engineering framework'
url='https://github.com/radareorg/r2cutter'
arch=('x86_64')
license=('GPL3')
depends=('radare2' 'capstone' 'qt5-base' 'qt5-svg' 'qt5-webengine' 'icu' 'python' 'jupyter'
         'pyside2' 'python-shiboken2' 'graphviz' 'gcc-libs' 'syntax-highlighting')
makedepends=('git' 'cmake' 'ninja' 'shiboken2' 'qt5-tools')
optdepends=('r2ghidra: ghidra decompiler plugin')
replaces=('radare2-cutter')
source=("git+https://github.com/radareorg/r2cutter#commit=${_gitcommit}"
        "r2cutter-translations::git+https://github.com/rizinorg/cutter-translations")
sha512sums=('SKIP'
            'SKIP')
b2sums=('SKIP'
        'SKIP')

#pkgver() {
#  cd r2cutter
#  git describe --tags --match 'v*' | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
#}

prepare() {
  cd r2cutter
  git config 'submodule.src/translations.url' "${srcdir}/r2cutter-translations"
  git submodule update --init src/translations
}

build() {
  cd r2cutter/src
  cmake -B build \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=None \
    -DCUTTER_ENABLE_PYTHON=ON \
    -DCUTTER_ENABLE_PYTHON_BINDINGS=ON \
    -DCUTTER_USE_BUNDLED_RADARE2=OFF \
    -DCUTTER_USE_ADDITIONAL_RADARE2_PATHS=OFF \
    -DCUTTER_ENABLE_CRASH_REPORTS=OFF \
    -DCUTTER_ENABLE_GRAPHVIZ=ON \
    -Wno-dev \
    -G Ninja
  ninja -C build
}

package() {
  cd r2cutter
  DESTDIR="${pkgdir}" ninja -C src/build install
  install -d "${pkgdir}/usr/share/doc/${pkgname}"
  cp -a docs/* "${pkgdir}/usr/share/doc/${pkgname}"
}

# vim: ts=2 sw=2 et:
