# Maintainer:  Kyle Keen <keenerd@gmail.com>
# Contributor: Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: schuay <jakob.gruber@gmail.com>
# Contributor: Tom K <tomk@runbox.com>
# Contributor: Thayer Williams <thayer@archlinux.org>

pkgname=epdfview
pkgver=0.1.8
pkgrel=11
pkgdesc='Lightweight PDF document viewer'
url='http://freecode.com/projects/epdfview'
arch=('x86_64')
license=('GPL')
depends=('poppler-glib' 'desktop-file-utils' 'hicolor-icon-theme' 'gtk2')
makedepends=('pkgconfig')
source=(ftp://ftp.slackware.com/.1/blfs/conglomeration/epdfview/$pkgname-$pkgver.tar.bz2
        epdfview-0.1.8-swap-the-blue-and-red-channel.patch
        epdfview-0.1.8-glib2-headers.patch
        epdfview-0.1.8-modern-cups.patch
        epdfview-0.1.8-pictures.patch)
md5sums=('e50285b01612169b2594fea375f53ae4'
         '7f9ea101a41f5b4e999fd024f423d41f'
         '2fffa9c7cd4c5f0744803591c2f162a3'
         '5c2cf5612d2a7dfe6cf005b94aeb5ada'
         '60064c976f273d86d6254c71b583cf4d')
sha256sums=('948648ae7c9d7b3b408d738bd4f48d87375b1196cae1129d6b846a8de0f2f8f0'
            'bfb0625a07e503edbea7528e02a94feac29d744600a9ddb5ce99a09710c4ba51'
            '8ea8c6d2c4badb6bfafc9fc550b8d877883c721b23de17322f0670ed07450a8e'
            '27529d78f4a042ad0fb2465ffe35e2018c5d75923f6523727dd2de0b70ccf296'
            'ecaaa44a13c0810b9d29595d8e317baf7253fbc6fb7fff388758d34a6eab1c8a')

prepare() {
  cd $pkgname-$pkgver
  sed -i 's/icon_epdfview-48/epdfview/' data/epdfview.desktop

  patch -p1 -i ../epdfview-0.1.8-swap-the-blue-and-red-channel.patch
  patch -p1 -i ../epdfview-0.1.8-glib2-headers.patch # FS#30116
  patch -p1 -i ../epdfview-0.1.8-modern-cups.patch   # FS#32511
  patch -p1 -i ../epdfview-0.1.8-pictures.patch      # FS#44936
}

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install

  for size in 24 32 48; do
    install -Dm644 data/icon_$pkgname-$size.png \
      "$pkgdir"/usr/share/icons/hicolor/${size}x${size}/apps/$pkgname.png
  done
}
