# Maintainer

pkgbase=python-bootstrap
pkgname=(python-build python-installer python-tomli python-pep517 python-setuptools python-flit-core python-wheel)
pkgver=0.1
pkgrel=1
arch=(any)
license=('custom')
makedepends=(python git)
url="https://gitlab.archlinux.org/archlinux/python-bootstrap"
# From archlinux-contrib
# package/parse-submodules https://github.com/jelly/python-bootstrap.git
source=(
  "${pkgbase}::git+https://github.com/jelly/python-bootstrap.git#branch=3.11"
  "${pkgbase}-build::git+https://github.com/pypa/build.git"
  "${pkgbase}-flit::git+https://github.com/takluyver/flit.git"
  "${pkgbase}-installer::git+https://github.com/pypa/installer.git"
  "${pkgbase}-pep517::git+https://github.com/pypa/pep517.git"
  "${pkgbase}-setuptools::git+https://github.com/pypa/setuptools.git"
  "${pkgbase}-tomli::git+https://github.com/hukkin/tomli.git"
  "${pkgbase}-wheel::git+https://github.com/pypa/wheel.git"
)
sha256sums=('SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP')

prepare() {
  cd python-bootstrap
  git submodule init

  git config submodule."external/build".url "${srcdir}/${pkgbase}"-build
  git config submodule."external/flit".url "${srcdir}/${pkgbase}"-flit
  git config submodule."external/installer".url "${srcdir}/${pkgbase}"-installer
  git config submodule."external/pep517".url "${srcdir}/${pkgbase}"-pep517
  git config submodule."external/setuptools".url "${srcdir}/${pkgbase}"-setuptools
  git config submodule."external/tomli".url "${srcdir}/${pkgbase}"-tomli
  git config submodule."external/wheel".url "${srcdir}/${pkgbase}"-wheel

  git -c protocol.file.allow=always submodule update
  git submodule update --init --recursive
}


build() {
  cd python-bootstrap
  python -m bootstrap.build
}

package_python-build() {
  depends=(python-pep517 python)
  pkgdesc="A simple, correct PEP 517 build frontend"
  cd python-bootstrap
  python -m bootstrap.install dist/build-*-py3-none-any.whl -d $pkgdir
}

package_python-installer() {
  depends=(python-pep517 python)
  pkgdesc="Low-level library for installing a Python package from a wheel distribution"
  cd python-bootstrap
  python -m bootstrap.install dist/installer-*-py3-none-any.whl -d $pkgdir
}

package_python-flit-core() {
  depends=(python-pep517 python)
  pkgdesc="Simplified packaging of Python modules (core backend)"
  cd python-bootstrap
  python -m bootstrap.install dist/flit_core-*-py3-none-any.whl -d $pkgdir
}

package_python-pep517() {
  pkgdesc="Wrappers to build Python packages using PEP 517 hooks"
  depends=(python)
  cd python-bootstrap
  python -m bootstrap.install dist/pep517-*-py3-none-any.whl -d $pkgdir
}

package_python-setuptools() {
  pkgdesc="Easily download, build, install, upgrade, and uninstall Python packages"
  depends=(python)
  cd python-bootstrap
  python -m bootstrap.install dist/setuptools-*-py3-none-any.whl -d $pkgdir
}

package_python-tomli() {
  pkgdesc="A lil' TOML parser"
  depends=(python)
  cd python-bootstrap
  python -m bootstrap.install dist/tomli-*-py3-none-any.whl -d $pkgdir
}

package_python-wheel() {
  pkgdesc="A built-package format for Python"
  depends=(python)
  cd python-bootstrap
  python -m bootstrap.install dist/wheel-*-py2.py3-none-any.whl -d $pkgdir
}

