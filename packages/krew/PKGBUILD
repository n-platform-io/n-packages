# Maintainer: Christian Rebischke <chris.rebischke@archlinux.org>
# Contributor: ml <ml CALLME visu.li>
pkgname=kubectl-krew
_pkgname=${pkgname#kubectl-}
pkgver=0.4.0
pkgrel=4
pkgdesc='Plugin manager for kubectl command-line tool'
arch=('x86_64')
url='https://krew.sigs.k8s.io/'
license=('Apache')
depends=('kubectl' 'git')
makedepends=('go')
provides=('krew')
groups=('kubectl-plugins')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/kubernetes-sigs/krew/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha512sums=('b59f3f18a29eb6b4cdafb0af6d3a5a88319e66573703907ebca1ffefcc65dfdb3c85c4da89a5eea9a45ebaa22c0b53ebe4068665b00fae639ec36cc8fe2b02c8')

build() {
  local _commit=
  _commit=$(bsdcat "${pkgname}-${pkgver}.tar.gz" | git get-tar-commit-id)
  local -a x=(
    sigs.k8s.io/krew/internal/version.gitCommit="${_commit:?}"
    sigs.k8s.io/krew/internal/version.gitTag="v${pkgver}"
  )
  cd "${_pkgname}-${pkgver}"
  export CGO_LDFLAGS="$LDFLAGS"
  export CGO_CFLAGS="$CFLAGS"
  export CGO_CPPFLAGS="$CPPFLAGS"
  export CGO_CXXFLAGS="$CXXFLAGS"
  export GOFLAGS='-buildmode=pie -trimpath -modcacherw -mod=readonly'
  go build -o . -ldflags "-linkmode=external ${x[*]/#/-X=}" ./cmd/{krew,validate-krew-manifest}
}

check() {
  cd "${_pkgname}-${pkgver}"
  # unit- and integrationtests
  KREW_BINARY="${PWD}/${pkgname#kubectl-}" go test -short ./...
}

package() {
  cd "${_pkgname}-${pkgver}"
  install -Dm755 krew -t "${pkgdir}/usr/bin"
  ln -srfT "$pkgdir/usr/bin/krew" "$pkgdir/usr/bin/kubectl-krew"
  install -m755 validate-krew-manifest -t "${pkgdir}/usr/bin"

  # docs
  install -Dm755 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
  cp -a docs/* -t "${pkgdir}/usr/share/doc/${pkgname}"
}
