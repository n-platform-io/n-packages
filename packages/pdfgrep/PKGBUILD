# Maintainer: schuay <jakob.gruber@gmail.com>
# Contributor: Gabriel B. Casella <gbc921@gmail.com>
# Contributor: josephgbr <rafael.f.f1@gmail.com>

pkgname=pdfgrep
pkgver=2.1.2
pkgrel=2
pkgdesc="A tool to search text in PDF files"
arch=('x86_64')
url="http://pdfgrep.sourceforge.net/"
license=('GPL')
depends=('poppler')
source=(https://pdfgrep.org/download/${pkgname}-${pkgver}.tar.gz)

build() {
  cd "$srcdir/$pkgname-$pkgver"
  ./configure --prefix=/usr
  make
}

check() {
  cd "$srcdir/$pkgname-$pkgver"
  make -k check
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir/" install
}

sha256sums=('0ef3dca1d749323f08112ffe68e6f4eb7bc25f56f90a2e933db477261b082aba')
