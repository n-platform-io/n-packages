# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=gnome-getting-started-docs
pkgver=3.38.1
pkgrel=1
pkgdesc="Help a new user get started in GNOME"
url="https://gitlab.gnome.org/GNOME/gnome-getting-started-docs"
license=(CCPL:by-sa)
arch=(any)
# gst plugins required to decode help videos
depends=(yelp gst-plugins-base gst-plugins-good)
makedepends=(yelp-tools git)
groups=(gnome)
_commit=b13e80f4abcf2addf48b8c426be3ed7b8e06fb85  # master
source=("git+https://gitlab.gnome.org/GNOME/gnome-getting-started-docs.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd $pkgname
  git tag -f 3.38.1 b13e80f4abcf2addf48b8c426be3ed7b8e06fb85 # Fixup missing tag
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd $pkgname
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var
  make
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
}
